class Theme {
    field int x, y;
    field int curPos;
    field Castle DogCastle, BadDogCastle;
    field LinkedList Dogs, BadDogs; 
    field GameUI ui;
    field Money money;

    constructor Theme new() {
        let Dogs = LinkedList.new(); // Number of dogs
        let BadDogs = LinkedList.new();
        let DogCastle = Castle.new(true); 
        let BadDogCastle = Castle.new(false);
        let ui = GameUI.new();
        let money = Money.new();
        return this;
    }

    method int getTargetDog() {
        var int targetDogIdx;
        var int idx;
        var int leftwall;
        var Dog cur;
        var Element it; // iterator
        let leftwall = 60; // dogcastle 
        let it = Dogs.getFront(); 
        let idx = 0;
        while(~(it = null)) {
            let cur = it.getData();
            if(cur.Location() + 60 > leftwall) {
                let leftwall = cur.Location() + 60;
                let targetDogIdx = idx;
            }
            let idx = idx + 1;
            let it = it.getNext(); 
        } 
        /** Set wall */
        do Dog.Change_leftWall(leftwall);
        return targetDogIdx;
    }

    method int getTargetBadDog() {
        var int targetBadDogIdx;
        var int idx;
        var int rightwall;
        var Dog cur;
        var Element it;
        let rightwall = 300; // baddogcastle
        let it = BadDogs.getFront(); 
        let idx = 0;
        while(~(it = null)) {
            let cur = it.getData();
            if(cur.Location() - 60 < rightwall) {
                let rightwall = cur.Location() - 60;
                let targetBadDogIdx = idx;
            }
            let idx = idx + 1;
            let it = it.getNext(); 
        }
        /** Set wall */
        do Dog.Change_rightWall(rightwall);
        return targetBadDogIdx;
    }

    method void run() {     
        var Element it;
        var Dog cur;
        var Dog targetDog, targetBadDog;
        var int targetDogIdx, targetBadDogIdx;
        var int damage;
        var int opt;

        let opt = -1;
        do Dog.Change_y(150);
        do Castle.Change_y(50);

        do Sys.wait(50);
        while(true) {
            do Sys.wait(100);

            /** For test purpose */
            if(BadDogs.size() < 3) {
                do BadDogs.push(Dog.new(0, false));
            }
             
            /** Find right most dog */
            let targetDogIdx = getTargetDog();   
            let targetDog = Dogs.getElement(targetDogIdx);

            /** Find left most baddog */
            let targetBadDogIdx = getTargetBadDog(); 
            let targetBadDog = BadDogs.getElement(targetBadDogIdx);

 
            /** Dog Action */
            let it = Dogs.getFront(); 
            while(~(it = null)) {
                let cur = it.getData();
                let damage = cur.act();
                if(damage > 0) {
                    if(targetBadDog = null) {
                        if(BadDogCastle.damage(damage)) {
                            do Screen.clearScreen();
                            do Output.printString("You win!");
                            do Sys.halt();    
                        }
                    } 
                    else {
                        if(targetBadDog.TakeAnyDamage(damage)) {
                            do BadDogs.pop(targetBadDogIdx);  
                            let targetBadDogIdx = getTargetBadDog();   
                            let targetBadDog = BadDogs.getElement(targetBadDogIdx);
                        }
                    }
                }
                let it = it.getNext();
            }
            let it = BadDogs.getFront(); 
            while(~(it = null)) {
                let cur = it.getData();
                let damage = cur.act(); 
                if(damage > 0) {
                    if(targetDog = null) { 
                        if(DogCastle.damage(damage)) {
                            do Screen.clearScreen();
                            do Output.printString("You lose!");
                            do Sys.halt();    
                        }
                    } 
                    else {
                        if(targetDog.TakeAnyDamage(damage)) {
                            do Dogs.pop(targetDogIdx); 
                            let targetDogIdx = getTargetDog();   
                            let targetDog = Dogs.getElement(targetDogIdx);
                        } 
                    }
                }
                let it = it.getNext();
            }

            /** Add money */
            do money.add();
            
            /** If clicked */
            let opt = ui.getopt();
            if(~opt) { 
                if(opt = 0) {
                    if(money.sub(10) = true) {
                        do Dogs.push(Dog.new(0, true)); 
                    }
                }
                else {
                    if(money.sub(15) = true) {
                        do Dogs.push(Dog.new(1, true));
                    }
                }
            }

            //** Start rendering */
            do Screen.clearScreen();

            /** Draw background */



            /** Draw Dogs */
            let it = Dogs.getFront(); 
            while(~(it = null)) {
                let cur = it.getData();
                do cur.show();
                let it = it.getNext();
            }
            let it = BadDogs.getFront(); 
            while(~(it = null)) {
                let cur = it.getData();
                do cur.show();
                let it = it.getNext();
            }

            /** Draw UI */
            do ui.display();

            /** Draw money */
            do money.draw();

            /** Draw blood */
            do DogCastle.draw();
            do BadDogCastle.draw();
        } 
        return;
    }

    method void dispose() {
        do DogCastle.dispose();
        do BadDogCastle.dispose();
        do Dogs.dispose();
        do BadDogs.dispose();
        do Memory.deAlloc(this);
        return;
    }
}
